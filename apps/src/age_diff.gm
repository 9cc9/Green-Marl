
// An example for GPS paper
// Find the difference of each node's avg and the average of its follower's age
// Find the difference of each node's avg and the average of its followee's age
Procedure agv_nbr_age(
        G: Graph, 
        age: N_P<Int>(G),
        diff1, diff2: N_P<Float>(G)
        )
{
    Foreach(n: G.Nodes) {
      Int sum = Sum(t:n.InNbrs) {t.age};
      Float avg;
      If (n.InDegree() != 0) {
        avg = sum / n.InDegree();
        n.diff1 = |n.age - avg|;
      } Else {
        avg = 0;
        n.diff1 = 0;
      }

      sum = Sum(t:n.OutNbrs) {t.age};
      If (n.OutDegree() != 0) {
        avg = sum / n.OutDegree();
        n.diff2 = |n.age - avg|;
      } Else {
        avg = 0;
        n.diff2 = 0;
      }
    }
}
