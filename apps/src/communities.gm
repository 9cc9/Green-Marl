Procedure communities(G : Graph, com : Node_Property<Int>)
{
	//as described in 'Near linear time algorithm to detect community structures in large-scale networks' by Raghavan et al
	//initialize the community for all nodes in the network
	Int com = 0;
	For(n : G.Nodes) {
		n.com = com;
		com++;
	}

	Bool finished = True;
	Do {	
		finished = True;
		Foreach(x : G.Nodes) {
			//adjust community
			Int max = Max(n : x.Nbrs) { Count(n2 : x.Nbrs) (n.com == n2.com) };			
			If(max > 0) {	// max <= 0 => x has no neighbors
				Foreach(n : x.Nbrs) ( max == Count(n2 : x.Nbrs) (n.com == n2.com) )
					x.com = n.com;
			}
		}
		Foreach(x : G.Nodes) {
			// additional iteration needed?
			Int max = Max(n : x.Nbrs) { Count(n2 : x.Nbrs) (n.com == n2.com) };
			Int myCount = Count(n : x.Nbrs) (x.com == n.com);
			If(myCount < max)
				finished = False;
		}
	} While(!finished);
}
